// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel RDFMain

RWStructuredBuffer<float2> Cells;
RWTexture2D<float4> Result;

int sizeL;

float dA;
float dB;
float feed;
float k;
float time;
float m;
int iterations;
int centerY;
int centerX;
float palette;

int kaleido;
static const float PI = 3.14159265f;

float map(float s, float a1, float a2, float b1, float b2)
{
    return b1 + (s-a1)*(b2-b1)/(a2-a1);
}

float3 pal( in float t, in float3 a, in float3 b, in float3 c, in float3 d )
{
    return a + b*cos( 6.28318*(c*t+d) );
}

float3 HUEtoRGB(in float H)
{
    float R = abs(H * 6 - 3) - 1;
    float G = 2 - abs(H * 6 - 2);
    float B = 2 - abs(H * 6 - 4);
    return saturate(float3(R,G,B));
}

float3 HSVtoRGB(in float3 HSV)
{
    float3 RGB = HUEtoRGB(HSV.x);
    return ((RGB - 1) * HSV.y + 1) * HSV.z;
}

[numthreads(32,32,1)]
void RDFMain (uint3 id : SV_DispatchThreadID)
{
    float hs = sizeL/2.0;
    float dist = distance(float2(id.x,id.y), float2(centerX,centerY));

    float2 spot = Cells[id.x + id.y * sizeL];

    float a = spot.x;
    float b = spot.y;

    float sm = sm = 0.05;
    m = 0.2;

    float laplaceA = 0;
    laplaceA += a * -1;
    laplaceA += Cells[id.x+1 + id.y*sizeL].x * m;
    laplaceA += Cells[id.x-1 + id.y*sizeL].x * m;
    laplaceA += Cells[id.x   + (id.y+1) * sizeL].x * m;
    laplaceA += Cells[id.x   + (id.y-1) * sizeL].x * m;
    laplaceA += Cells[id.x-1 + (id.y-1) * sizeL].x * sm;
    laplaceA += Cells[id.x+1 + (id.y-1) * sizeL].x * sm;
    laplaceA += Cells[id.x-1 + (id.y+1) * sizeL].x * sm;
    laplaceA += Cells[id.x+1 + (id.y+1) * sizeL].x * sm;

    float laplaceB = 0;
    laplaceB += b*-1;
    laplaceB += Cells[id.x+1 + id.y*sizeL].y * m;
    laplaceB += Cells[id.x-1 + id.y*sizeL].y * m;
    laplaceB += Cells[id.x   + (id.y+1) * sizeL].y * m;
    laplaceB += Cells[id.x   + (id.y-1) * sizeL].y * m;
    laplaceB += Cells[id.x-1 + (id.y-1) * sizeL].y * sm;
    laplaceB += Cells[id.x+1 + (id.y-1) * sizeL].y * sm;
    laplaceB += Cells[id.x-1 + (id.y+1) * sizeL].y * sm;
    laplaceB += Cells[id.x+1 + (id.y+1) * sizeL].y * sm;

    dB = map(dist*dist,hs*hs*2,0,0.1, dB);
    spot.x = a + (dA * laplaceA - a*b*b + feed * (1-a)) * 1;
    spot.y = b + (dB * laplaceB + a*b*b - (k + feed)*b) * 1;

    spot.x = clamp(spot.x, 0.0, 1.0);
    spot.y = clamp(spot.y, 0.0, 1.0);

    float v = (spot.x - spot.y);

    Cells[id.x + id.y * sizeL].x = spot.x;
    Cells[id.x + id.y * sizeL].y = spot.y;

    float t = time * 0.07;
    float h = map((sin((v + t) * 4) + 1) / 2.0, 0, 1, 0, 0.8);// 0.667);
    float s = map((sin(v*time)+1)/2.0, 0, 1, 0.4, 1);
    float3 rgb = HSVtoRGB(float3(h, s, 1));

    // this blends between two palettes:
    // https://iquilezles.org/articles/palettes/
    float3 cc = float3(1.0,
            map(palette, 0.0, 1.0, 0.7, 1.0),
            map(palette, 0.0, 1.0, 0.4, 0.5));
    float3 dc = float3(map(palette, 0.0, 1.0, 0.0, 0.8),
        map(palette, 0.0, 1.0, 0.15, 0.9),
        map(palette, 0.0, 1.0, 0.2, 0.3));
    
    //float3 col = pal((v + time*0.05) % 1.0,
    //   float3(0.5,0.5,0.5),
    //   float3(0.5,0.5,0.5),
    //   cc,
    //   dc);
    float3 col = pal(fmod(v + time*0.05, 1.0),
       float3(0.5,0.5,0.5),
       float3(0.5,0.5,0.5),
       cc,
       dc);

    Result[id.xy] = float4(col, 1.0);//float4(rgb.x,rgb.y,rgb.z,1);
}